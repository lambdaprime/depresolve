plugins {
  id 'com.diffplug.spotless'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  testImplementation(project ":depresolve")
  testImplementation "io.github.lambdaprime:id.xfunction:25.0"

  def junitVersion = "5.8.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}

// Gradle has no UnZip task so we reuse Copy for that
task myTest(type: Copy) {
  from zipTree("${project.rootDir}/${projectName}/release/${releaseFileName}")
  into "$buildDir"
}

test {
  useJUnitPlatform()

  testLogging {
    events "started", "failed"
  }
}

myTest.dependsOn project(':depresolve').createRelease
compileTestJava.dependsOn myTest
jar.dependsOn myTest

// run spotless and format code before the build
testClasses.dependsOn spotlessApply
